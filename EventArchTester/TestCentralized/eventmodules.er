//--------------------------------------- Specifications of Event Types --------------------------------
eventtype BaseEventType{
		 String publisher;
		 String publisherGroup;
		 String publisherInstance;
		 String target;
		 String targetGroup;
		 String targetInstance;
		 Long timestamp;
		 Long thread;
		 String scope;
}

eventtype CoordinationCommand extends BaseEventType{
		 String command;
}
eventtype ConstituentState extends BaseEventType{
		 String applicationState;
		 String serverLoad;
}
eventtype ApplicationTerminated extends BaseEventType{
	String name;
}
eventtype ApplicationStarted extends BaseEventType{
	String name;
}
//--------------------------------------- Specifications of Modules --------------------------------
modules {
	WrappedLoadBalancer[Java]:= {ISwitchCoordinatorLB,IStateCoordinatorLB} <->{'LoadBalancer'}
	WrappedAdaptiveSoftware[Java]:= {ISwitchCoordinatorAS,IStateCoordinatorAS} <->{'AdaptiveSoftware'}
	
	SwitchCoordinator[StateMachine] := {ISwitchCoordinator} <->  { 
		initial state Start { 
			during:
				on (ISwitchCoordinator.e_StartedLB) -> WaitForSwitch; 
				on (ISwitchCoordinator.e_TerminatedAS) -> WaitForRestart;
			exit:
				send ISwitchCoordinator.e_CoordinationCMD = new CoordinationCommand(){
					command = 'suspend';
					target = 'WrappedLoadBalancer';
				};	 
		} 
		state WaitForSwitch { 
			entry:
				 send ISwitchCoordinator.e_CoordinationCMD = new CoordinationCommand() {
					target = 'WrappedAdaptiveSoftware';	command = 'switch'; };
			during:	 
					on (ISwitchCoordinator.e_EndedSwitch) ->Start; 
					on (ISwitchCoordinator.e_TerminatedAS) -> WaitForRestart;
			exit:
				send ISwitchCoordinator.e_CoordinationCMD = new CoordinationCommand(){command = 'proceed'; 		target = 'WrappedLoadBalancer';};
		}
		state WaitForRestart { 
			during:	 
				on (ISwitchCoordinator.e_InitializedAS) -> Start;
				on (ISwitchCoordinator.e_StartedLB){
					send ISwitchCoordinator.e_CoordinationCMD = new	CoordinationCommand(){command = 'proceed';target = 'WrappedLoadBalancer';};
				}
		}
	}
	StateCoordinator[StateMachine] := {IStateCoordinator} <->  {
		initial state Start {
				during:
					on (IStateCoordinator.e_StartedAS) {
							print ">>>>>  we are starting application >>>>>>>";
							send IStateCoordinator.e_CoordinationCMD = new CoordinationCommand(){
	 							command = 'proceed';
	 							target ='WrappedAdaptiveSoftware';
	 						  };
	 						} -> RunAS;
					
					on (IStateCoordinator.e_StartedLB_UnderUtilized) {
	 						send IStateCoordinator.e_CoordinationCMD = new CoordinationCommand(){
	 							command = 'proceed';
	 							target ='WrappedLoadBalancer';
	 						}; 
					}-> RunLB;
		} 
		state RunAS {
				during:
					on (IStateCoordinator.e_StartedLB_UnderUtilized) {
	 					send IStateCoordinator.e_CoordinationCMD = new CoordinationCommand(){
	 							command = 'suspend';
	 							target ='WrappedLoadBalancer';
	 						};
	 				} ->SuspendLB;
	 				 
					on (IStateCoordinator.e_EndedAS) -> Start;
		}
		state SuspendLB {
				during:
					on (IStateCoordinator.e_EndedAS) { 
	 						send IStateCoordinator.e_CoordinationCMD = new CoordinationCommand(){
	 							command = 'proceed';
	 							target ='WrappedLoadBalancer';
	 						};
	 				} ->RunLB;
	 		}
		state RunLB{
				during:
					on (IStateCoordinator.e_StartedAS){ 
							send IStateCoordinator.e_CoordinationCMD = new CoordinationCommand(){
	 							command = 'suspend';
	 							target ='WrappedAdaptiveSoftware';
	 						};
	 				} -> SuspendAS;
					on (IStateCoordinator.e_EndedLB_UnderUtilized) -> Start;
		}
		state SuspendAS{
			during:
				on (IStateCoordinator.e_EndedLB_UnderUtilized) {
	 						send IStateCoordinator.e_CoordinationCMD = new CoordinationCommand(){
	 							command = 'proceed';
	 							target ='WrappedAdaptiveSoftware';
	 						};
	 				}  -> RunAS;
		 }
	}
}
//--------------------------------------- Specifications of Load Balancer Interfaces --------------------------------
interface ISwitchCoordinatorLB for WrappedLoadBalancer{
	requires {
		CoordinationCommand e_CoordinationCMD = {E | E instanceof 'CoordinationCommand' &&
								 E.publisher== 'SwitchCoordinator' &&
								 E.target == 'WrappedLoadBalancer'}
	}
	provides {
		ConstituentState e_StartedLB := 
				before	execution (void org.dummy.loadbalancer.MigrationPlanner.plan(String load)) 
				if	(load =='OverUtilized') 
				{ 
					serverLoad = load;
					applicationState = 'StartExecuting';
					target = 'SwitchCoordinator';
				}
		wait when (e_StartedLB) until (e_CoordinationCMD){
				switch (e_CoordinationCMD.command){
					case 'proceed': proceed;
					case 'suspend':	suspend;
				}			
		}
	}
}
interface IStateCoordinatorLB for WrappedLoadBalancer{
	requires {
		CoordinationCommand e_CoordinationCMD = {E | E instanceof 'CoordinationCommand' &&
												E.publisher == 'StateCoordinator' && 
												E.target == 'WrappedLoadBalancer'}
	}	
	provides {		
		ConstituentState e_StartedLB := 
				before execution (void org.dummy.loadbalancer.MigrationPlanner.plan(String load)) 
						if	(load =='UnderUtilized') { 
							serverLoad = load; 
							applicationState = 'StartExecuting';
							target = 'StateCoordinator';
						}
		wait when (e_StartedLB) until (e_CoordinationCMD){
			switch (e_CoordinationCMD.command){
					case 'proceed': proceed;
					case 'suspend': suspend;
				}			
		}
		ConstituentState e_EndedLB := after execution (void org.dummy.loadbalancer.Migrator.migrate(..)){ 
				applicationState = 'EndExecuting';
				target = 'StateCoordinator';
				serverLoad = 'UnderUtilized'; 
				
		}
	}
}
//--------------------------------------- Specifications of Adaptive Software Interfaces --------------------------------
functional interface MyInterface for WrappedAdaptiveSoftware{
	provides {
		port execute in org.dummy.application.ApplicationComponent;
	}
}

interface ISwitchCoordinatorAS for WrappedAdaptiveSoftware{
	requires {
		CoordinationCommand e_CoordinationCMD = {E | E instanceof	'CoordinationCommand' && 
													 E.publisher == 'SwitchCoordinator' &&
													 E.target == 'WrappedAdaptiveSoftware'}
		on (e_CoordinationCMD) {invoke ('org.dummy.application.Optimizer', 'reconfigure',e_CoordinationCMD); }
	}	
	provides { 
		ConstituentState e_EndSwitch := 
			after execution (static void org.dummy.application.Optimizer.reconfigure(..)) {	
					applicationState = 'SwitchEnd';	}
	}					
}
interface IStateCoordinatorAS for WrappedAdaptiveSoftware{
requires {
		CoordinationCommand e_CoordinationCMD = {E | E instanceof 'CoordinationCommand' &&
												E.publisher == 'StateCoordinator' &&
												E.target == 'WrappedAdaptiveSoftware'}
	}	
	provides {		
		ConstituentState e_StartedAS := 
				before execution (void org.dummy.application.ApplicationComponent.execute(..)) 
						{ 
							applicationState = 'StartExecuting';
							target = 'StateCoordinator';
						}
		wait when (e_StartedAS) until (e_CoordinationCMD){
			switch (e_CoordinationCMD.command){
					case 'proceed': proceed;
					case 'suspend': suspend;
				}			
		}
		ConstituentState e_EndedAS := after execution (void org.dummy.application.ApplicationComponent.execute(..)){ 
				applicationState = 'EndExecuting';
				target = 'StateCoordinator';
				
		}
	}}
//--------------------------------- Specifications of Coordinator Interfaces -------------------------
interface ISwitchCoordinator for SwitchCoordinator{
  	requires {
		ConstituentState e_StartedLB= {E | E instanceof	'ConstituentState'	&& 
										  E.publisher == 'WrappedLoadBalancer' && 
										  E.applicationState == 'StartExecuting' &&
										  E.serverLoad == 'OverUtilized'}
		ConstituentState e_EndedSwitch = {E | E instanceof 'ConstituentState' &&	
											 E.publisher == 'WrappedAdaptiveSoftware' && 
											 E.applicationState	=='SwitchEnd'}
		ApplicationTerminated e_TerminatedAS = {E | E instanceof 'ApplicationTerminated' && 
												E.name == 'WrappedAdaptiveSoftware'} 
		ApplicationStarted e_InitializedAS = {E | E instanceof 'ApplicationStarted' &&
										  E.name == 'WrappedAdaptiveSoftware'}													 										  
	} 
	provides { CoordinationCommand e_CoordinationCMD;} 
}				

interface IStateCoordinator for StateCoordinator{
requires {
		ConstituentState e_StartedLB_UnderUtilized = {E | E instanceof 'ConstituentState' && 
			 										   E.publisher == 'WrappedLoadBalancer' && 
										  			   E.applicationState == 'StartExecuting' && 
									  				   E.serverLoad == 'UnderUtilized'}

		ConstituentState e_EndedLB_UnderUtilized = {E | E instanceof 'ConstituentState' && 
			 										   E.publisher == 'WrappedLoadBalancer' && 
										  			   E.applicationState == 'EndExecuting' && 
									  				   E.serverLoad == 'UnderUtilized'}
									  				   
		ConstituentState e_StartedAS = {E | E instanceof 'ConstituentState' && 
			 										   E.publisher == 'WrappedAdaptiveSoftware' && 
										  			   E.applicationState == 'StartExecuting'}
		
		ConstituentState e_EndedAS = {E | E instanceof 'ConstituentState' && 
			 										   E.publisher == 'WrappedAdaptiveSoftware' && 
										  			   E.applicationState == 'EndExecuting'}
	}
provides { CoordinationCommand e_CoordinationCMD;} 
	
}				
